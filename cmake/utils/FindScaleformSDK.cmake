# - Find Scaleform SDK
# Try to find the Scaleform SDK libraries (libgfx and bundled dependancies)
# Once done, this will define
#	SCALEFORMSDK_FOUND - System has the Scaleform SDK
#	SCALEFORMSDK_INCLUDE_DIRS - The Scaleform SDK include directories
#	SCALEFORMSDK_LIBRARIES - The libraries needed to use the Scaleform SDK

# Based on http://www.cmake.org/Wiki/CMake:How_To_Find_Libraries
# and FindALSA.cmake, which is apparently current best-practice

# TODO: Pick renderer using components syntax

FIND_PATH( SCALEFORMSDK_ROOT_DIR 
	Include/GFxVersion.h
	PATHS	${BW_SOURCE_DIR}/third_party/scaleform_gfxsdk
			${BW_SOURCE_DIR}/third_party/scaleform_sdk
)

# Scaleform SDK Include dirs
FIND_PATH( SCALEFORMSDK_INCLUDE_DIR
	GFxVersion.h "${SCALEFORMSDK_ROOT_DIR}/Include"
)

# Extract the version from GFX_VERSION_STRING in GFxVersion.h
IF( SCALEFORMSDK_INCLUDE_DIR AND EXISTS "${SCALEFORMSDK_INCLUDE_DIR}/GFxVersion.h" )
	FILE( STRINGS "${SCALEFORMSDK_INCLUDE_DIR}/GFxVersion.h"
		_SCALEFORMSDK_VERSION_STR
		REGEX "^#define[\t ]+GFX_VERSION_STRING[\t ]+\".*\""
	)
	STRING( REGEX REPLACE "^.*GFX_VERSION_STRING[\t ]+\"([^\"]*)\".*$" "\\1"
		SCALEFORMSDK_VERSION_STRING ${_SCALEFORMSDK_VERSION_STR}
	)
	UNSET( _SCALEFORMSDK_VERSION_STR )
ENDIF()

# Extract the 3rd-party libraries enabled by GFxVersion.
IF( SCALEFORMSDK_INCLUDE_DIR )
	FILE( STRINGS "${SCALEFORMSDK_INCLUDE_DIR}/GFxConfig.h"
		_SCALEFORMSDK_CONFIG_STRS
		REGEX "^#define[\t ]+SF_ENABLE_\(LIBJPEG|ZLIB|LIBPNG|PCRE\)$"
	)

	IF( _SCALEFORMSDK_CONFIG_STRS MATCHES "#define[\t ]+SF_ENABLE_LIBJPEG" )
		SET( SCALEFORMSDK_USES_LIBJPEG TRUE )
	ELSE()
		SET( SCALEFORMSDK_USES_LIBJPEG FALSE )
	ENDIF()

	IF( _SCALEFORMSDK_CONFIG_STRS MATCHES "#define[\t ]+SF_ENABLE_ZLIB" )
		SET( SCALEFORMSDK_USES_ZLIB TRUE )
	ELSE()
		SET( SCALEFORMSDK_USES_ZLIB FALSE )
	ENDIF()

	IF( _SCALEFORMSDK_CONFIG_STRS MATCHES "#define[\t ]+SF_ENABLE_LIBPNG" )
		SET( SCALEFORMSDK_USES_LIBPNG TRUE )
	ELSE()
		SET( SCALEFORMSDK_USES_LIBPNG FALSE )
	ENDIF()
	
	IF( _SCALEFORMSDK_CONFIG_STRS MATCHES "#define[\t ]+SF_ENABLE_PCRE" )
		SET( SCALEFORMSDK_USES_PCRE TRUE )
	ELSE()
		SET( SCALEFORMSDK_USES_PCRE FALSE )
	ENDIF()

	UNSET( _SCALEFORMSDK_CONFIG_STRS )
ENDIF()

# Setup compiler and platform-specific directory prefixes
IF( MSVC )
	IF( BW_ARCH_32 )
		SET( SCALEFORMSDK_LIBRARY_DIR
			${SCALEFORMSDK_ROOT_DIR}/Lib/Win32/Msvc90
		)
		SET( SCALEFORMSDK_LIBRARY_LIBJPEG_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/jpeg-6b/lib/Win32/Msvc90
		)
		SET( SCALEFORMSDK_LIBRARY_LIBPNG_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/libpng/Lib/Win32/Msvc90
		)
		SET( SCALEFORMSDK_LIBRARY_ZLIB_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/zlib-1.2.5/lib/win32/Msvc90
		)
		SET( SCALEFORMSDK_LIBRARY_PCRE_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/pcre/lib/win32/Msvc90
		)
	ELSE()
		SET( SCALEFORMSDK_LIBRARY_DIR
			${SCALEFORMSDK_ROOT_DIR}/Lib/x64/Msvc10
		)
		SET( SCALEFORMSDK_LIBRARY_LIBJPEG_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/jpeg-6b/lib/x64/Msvc90
		)
		SET( SCALEFORMSDK_LIBRARY_LIBPNG_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/libpng/Lib/x64/Msvc90
		)
		SET( SCALEFORMSDK_LIBRARY_ZLIB_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/zlib-1.2.5/lib/x64/Msvc90
		)
		SET( SCALEFORMSDK_LIBRARY_PCRE_DIR
			${SCALEFORMSDK_ROOT_DIR}/3rdParty/pcre/lib/x64/Msvc90
		)
	ENDIF()
	SET( SCALEFORMSDK_LIBRARY_SRC_DIR
		${SCALEFORMSDK_ROOT_DIR}/Src
		${SCALEFORMSDK_ROOT_DIR}/Src/Gfx
		${SCALEFORMSDK_ROOT_DIR}/Src/GFxIME
	)
ELSE()
	MESSAGE( FATAL_ERROR "CMake bwclient for '${CMAKE_GENERATOR}' not currently supported by BigWorld." )
ENDIF()

FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX
	libgfx.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX
	libgfx.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)

FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX_EXPAT
	libgfxexpat.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX_EXPAT
	libgfxexpat.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)

FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_LIBGRENDERER_D3D9
	libgfxrender_d3d9.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_LIBGRENDERER_D3D9
	libgfxrender_d3d9.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)

FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_IME
	libgfx_ime.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_IME
	libgfx_ime.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)


FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_GFXVIDEO
	libgfxvideo.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_GFXVIDEO
	libgfxvideo.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)

FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_AS2
	libgfx_as2.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_AS2
	libgfx_as2.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)

FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_AS3
	libgfx_as3.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_AS3
	libgfx_as3.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)

FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_SOUND
	libgfxsound_fmod.lib ${SCALEFORMSDK_LIBRARY_DIR}/Release
)
FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_SOUND
	libgfxsound_fmod.lib ${SCALEFORMSDK_LIBRARY_DIR}/Debug
)

IF( SCALEFORMSDK_USES_LIBJPEG )
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_LIBJPEG
		libjpeg.lib ${SCALEFORMSDK_LIBRARY_LIBJPEG_DIR}/Release
	)
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_LIBJPEG
		libjpeg.lib ${SCALEFORMSDK_LIBRARY_LIBJPEG_DIR}/Debug
	)
ELSE()
	# To pass FIND_PACKAGE_HANDLE_STANDARD_ARGS
	SET( SCALEFORMSDK_LIBRARY_RELEASE_LIBJPEG TRUE )
	SET( SCALEFORMSDK_LIBRARY_DEBUG_LIBJPEG TRUE )
ENDIF()

IF( SCALEFORMSDK_USES_LIBPNG )
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_LIBPNG
		libpng.lib ${SCALEFORMSDK_LIBRARY_LIBPNG_DIR}/Release
	)
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_LIBPNG
		libpng.lib ${SCALEFORMSDK_LIBRARY_LIBPNG_DIR}/Debug
	)
ELSE()
	# To pass FIND_PACKAGE_HANDLE_STANDARD_ARGS
	SET( SCALEFORMSDK_LIBRARY_RELEASE_LIBPNG TRUE )
	SET( SCALEFORMSDK_LIBRARY_DEBUG_LIBPNG TRUE )
ENDIF()

IF( SCALEFORMSDK_USES_ZLIB )
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_ZLIB
		zlib.lib ${SCALEFORMSDK_LIBRARY_ZLIB_DIR}/Release
	)
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_ZLIB
		zlib.lib ${SCALEFORMSDK_LIBRARY_ZLIB_DIR}/Debug
	)
ELSE()
	# To pass FIND_PACKAGE_HANDLE_STANDARD_ARGS
	SET( SCALEFORMSDK_LIBRARY_RELEASE_ZLIB TRUE )
	SET( SCALEFORMSDK_LIBRARY_DEBUG_ZLIB TRUE )
ENDIF()

IF( SCALEFORMSDK_USES_PCRE )
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_RELEASE_PCRE
		pcre.lib ${SCALEFORMSDK_LIBRARY_PCRE_DIR}/Release
	)
	FIND_LIBRARY( SCALEFORMSDK_LIBRARY_DEBUG_PCRE
		pcre.lib ${SCALEFORMSDK_LIBRARY_PCRE_DIR}/Debug
	)
ELSE()
	# To pass FIND_PACKAGE_HANDLE_STANDARD_ARGS
	SET( SCALEFORMSDK_LIBRARY_RELEASE_PCRE TRUE )
	SET( SCALEFORMSDK_LIBRARY_DEBUG_PCRE TRUE )
ENDIF()

INCLUDE( FindPackageHandleStandardArgs )

# Note that we exclude libpng because BigWorld already
# statically links a copy.

FIND_PACKAGE_HANDLE_STANDARD_ARGS( ScaleformSDK
	REQUIRED_VARS
		# Top of SDK (appears in the message output)
		SCALEFORMSDK_ROOT_DIR 
		# Headers
		SCALEFORMSDK_INCLUDE_DIR
		# Release libraries
		SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX
		SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX_EXPAT
		SCALEFORMSDK_LIBRARY_RELEASE_LIBGRENDERER_D3D9
		SCALEFORMSDK_LIBRARY_RELEASE_LIBJPEG
		SCALEFORMSDK_LIBRARY_RELEASE_PCRE
#		SCALEFORMSDK_LIBRARY_RELEASE_LIBPNG
		SCALEFORMSDK_LIBRARY_RELEASE_ZLIB
		SCALEFORMSDK_LIBRARY_RELEASE_IME
		SCALEFORMSDK_LIBRARY_RELEASE_GFXVIDEO
		SCALEFORMSDK_LIBRARY_RELEASE_AS2
		SCALEFORMSDK_LIBRARY_RELEASE_AS3
		SCALEFORMSDK_LIBRARY_RELEASE_SOUND
		# Debug libraries
		SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX
		SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX_EXPAT
		SCALEFORMSDK_LIBRARY_DEBUG_LIBGRENDERER_D3D9
		SCALEFORMSDK_LIBRARY_DEBUG_LIBJPEG
		SCALEFORMSDK_LIBRARY_DEBUG_PCRE
#		SCALEFORMSDK_LIBRARY_DEBUG_LIBPNG
		SCALEFORMSDK_LIBRARY_DEBUG_ZLIB
		SCALEFORMSDK_LIBRARY_DEBUG_IME
		SCALEFORMSDK_LIBRARY_DEBUG_GFXVIDEO
		SCALEFORMSDK_LIBRARY_DEBUG_AS2
		SCALEFORMSDK_LIBRARY_DEBUG_AS3
		SCALEFORMSDK_LIBRARY_DEBUG_SOUND
	VERSION_VAR SCALEFORMSDK_VERSION_STRING
)

IF( SCALEFORMSDK_FOUND )
	SET( SCALEFORMSDK_INCLUDE_DIRS
		${SCALEFORMSDK_INCLUDE_DIR}
		${SCALEFORMSDK_LIBRARY_SRC_DIR}
	)

	# Yes, these keywords need to be lower-case, or they are
	# interpreted as library names.
	SET( SCALEFORMSDK_LIBRARIES
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX}
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX_EXPAT}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX_EXPAT}
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_LIBGRENDERER_D3D9}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_LIBGRENDERER_D3D9}
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_IME}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_IME}
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_GFXVIDEO}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_GFXVIDEO}
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_AS2}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_AS2}
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_AS3}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_AS3}
		optimized ${SCALEFORMSDK_LIBRARY_RELEASE_SOUND}
		debug ${SCALEFORMSDK_LIBRARY_DEBUG_SOUND}
	)

	IF( SCALEFORMSDK_USES_LIBJPEG )
		SET( SCALEFORMSDK_LIBRARIES ${SCALEFORMSDK_LIBRARIES}
			optimized ${SCALEFORMSDK_LIBRARY_RELEASE_LIBJPEG}
			debug ${SCALEFORMSDK_LIBRARY_DEBUG_LIBJPEG}
		)
	ENDIF()

	IF( SCALEFORMSDK_USES_LIBPNG )
#		SET( SCALEFORMSDK_LIBRARIES ${SCALEFORMSDK_LIBRARIES}
#			optimized ${SCALEFORMSDK_LIBRARY_RELEASE_LIBPNG}
#			debug ${SCALEFORMSDK_LIBRARY_DEBUG_LIBPNG}
#		)
	ENDIF()

	IF( SCALEFORMSDK_USES_ZLIB )
		SET( SCALEFORMSDK_LIBRARIES ${SCALEFORMSDK_LIBRARIES}
			optimized ${SCALEFORMSDK_LIBRARY_RELEASE_ZLIB}
			debug ${SCALEFORMSDK_LIBRARY_DEBUG_ZLIB}
		)
	ENDIF()
	
	IF( SCALEFORMSDK_USES_PCRE )
		SET( SCALEFORMSDK_LIBRARIES ${SCALEFORMSDK_LIBRARIES}
			optimized ${SCALEFORMSDK_LIBRARY_RELEASE_PCRE}
			debug ${SCALEFORMSDK_LIBRARY_DEBUG_PCRE}
		)
	ENDIF()
ENDIF()

MARK_AS_ADVANCED( 
	SCALEFORMSDK_USES_LIBJPEG
	SCALEFORMSDK_USES_LIBPNG
	SCALEFORMSDK_USES_ZLIB
	SCALEFORMSDK_INCLUDE_DIR
	SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX
	SCALEFORMSDK_LIBRARY_RELEASE_LIBGFX_EXPAT
	SCALEFORMSDK_LIBRARY_RELEASE_LIBGRENDERER_D3D9
	SCALEFORMSDK_LIBRARY_RELEASE_LIBJPEG
	SCALEFORMSDK_LIBRARY_RELEASE_PCRE
	SCALEFORMSDK_LIBRARY_RELEASE_LIBPNG
	SCALEFORMSDK_LIBRARY_RELEASE_ZLIB
	SCALEFORMSDK_LIBRARY_RELEASE_IME
	SCALEFORMSDK_LIBRARY_RELEASE_GFXVIDEO
	SCALEFORMSDK_LIBRARY_RELEASE_AS2
	SCALEFORMSDK_LIBRARY_RELEASE_AS3
	SCALEFORMSDK_LIBRARY_RELEASE_SOUND
	SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX
	SCALEFORMSDK_LIBRARY_DEBUG_LIBGFX_EXPAT
	SCALEFORMSDK_LIBRARY_DEBUG_LIBGRENDERER_D3D9
	SCALEFORMSDK_LIBRARY_DEBUG_LIBJPEG
	SCALEFORMSDK_LIBRARY_DEBUG_PCRE
	SCALEFORMSDK_LIBRARY_DEBUG_LIBPNG
	SCALEFORMSDK_LIBRARY_DEBUG_ZLIB
	SCALEFORMSDK_LIBRARY_DEBUG_IME
	SCALEFORMSDK_LIBRARY_DEBUG_GFXVIDEO
	SCALEFORMSDK_LIBRARY_DEBUG_AS2
	SCALEFORMSDK_LIBRARY_DEBUG_AS3
	SCALEFORMSDK_LIBRARY_DEBUG_SOUND
)
